AC_INIT(gparted, 0.3.1)
AC_CONFIG_SRCDIR(src/main.cc)
AM_INIT_AUTOMAKE

AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE


dnl======================
dnl checks for programs 
dnl======================
AC_PROG_CC
AC_PROG_CXX
AM_PROG_LIBTOOL

dnl======================
dnl i18n stuff 
dnl======================
GETTEXT_PACKAGE=gparted
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",[description])

ALL_LINGUAS="bg ca cs de dz el en_CA en_GB es fi fr gl he hu it ja lt lv mk nb ne nl pa pl pt pt_BR ru rw sl sv tr uk vi zh_CN zh_TW"

AM_GLIB_GNU_GETTEXT
AC_PROG_INTLTOOL


dnl======================
dnl checks for libs 
dnl======================
AC_CHECK_LIB(uuid, uuid_generate, [], AC_MSG_ERROR([*** uuid library (libuuid) not found]))
AC_CHECK_LIB(dl, dlopen,          [], AC_MSG_ERROR([*** dl library (libdl) not found]))

dnl libparted
LIBPARTED_VERSION=1.7.1
AC_MSG_CHECKING(for libparted >= $LIBPARTED_VERSION)
LIBS_save="$LIBS"
LIBS="-lparted"
AC_TRY_RUN(
#include <stdio.h>
#include <parted/parted.h>

int main ()
{
	int min_major ;
	int min_minor ;
	int min_micro ;
	int major ;
	int minor ;
	int micro ;

	if ( sscanf( "$LIBPARTED_VERSION", "%d.%d.%d", &min_major, &min_minor, &min_micro ) == 3 )
	{
		if ( sscanf( ped_get_version(), "%d.%d.%d", &major, &minor, &micro ) == 3 )
		{
			printf( "Found libparted %s\t", ped_get_version() ) ;

			return ! ((major > min_major) ||
           			 ((major == min_major) && (minor > min_minor)) ||
           			 ((major == min_major) && (minor == min_minor) && (micro >= min_micro))) ;
		}
	}

	return 1 ;
}
,AC_MSG_RESULT(OK),AC_MSG_ERROR(*** Requires libparted >= $LIBPARTED_VERSION) )
LIBS="$LIBS_save"


dnl GTKMM
PKG_CHECK_MODULES(GTKMM, gtkmm-2.4 > 2.8 )
AC_SUBST(GTKMM_LIBS)
AC_SUBST(GTKMM_CFLAGS)


AC_OUTPUT([
Makefile
compose/Makefile
include/Makefile
src/Makefile
pixmaps/Makefile
po/Makefile.in
])
